entrypoints Program;
Progr. Program ::= [Stm] ;

comment "/*" "*/" ;
comment "//";

token CIdent (letter (letter | digit | '_')*) ;

SIf.           Stm ::= "[" "If" Exp [Stm] "]";
SIfElse.       Stm ::= "[" "If" Exp [Stm] "|" "Else" [Stm] "]" ;
SJump.         Stm ::= JumpStm ;
SExp.          Stm ::= Exp ;
SFunDef.       Stm ::= FunDeclaration ;
SExec.         Stm ::= "[" "Exec" Stm "]" ;

separator Stm "" ;

SjumpReturn.      JumpStm ::= "[" "Return" "]" ;
SjumpReturnV.     JumpStm ::= "[" "Return" Exp "]" ;
SjumpBreak.       JumpStm ::= "[" "Break" "]" ;
SjumpBreakV.      JumpStm ::= "[" "Break" Exp "]" ;
SjumpContinue.    JumpStm ::= "[" "Continue" "]";
SjumpContinueV.   JumpStm ::= "[" "Continue" Exp "]" ;

ENone.          Exp ::= "[" "]";
EFunNone.       Exp ::= "[" "None" [Stm] "]";
EAsign.         Exp ::= "[" "Let" CIdent Exp "]";
EYield.         Exp ::= "[" "Yield" Exp "]" ;
ETrue.          Exp ::= "[" "True" "]" ;
EFalse.         Exp ::= "[" "False" "]" ;
EFor.           Exp ::= "[" "For" CIdent Exp Exp [Stm] "]" ;
EWhile.         Exp ::= "[" "While" Exp [Stm] "]" ;
EInt.           Exp ::= Integer ;
ENegInt.        Exp ::= "-" Integer ;
EString.        Exp ::= String ;
EIdent.         Exp ::= CIdent ;
EFunPar.        Exp ::= "[" Exp [Exp] "]" ;

separator Exp "" ;

FunParam. FunParam ::= "@" CIdent ;
separator FunParam "" ;
FunDec. FunDeclaration ::= "[" "Def" CIdent [FunParam] [Stm] "]";
separator FunDeclaration "" ;
